plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
}

def requiredProp = { key, fallback = null ->
    if (project.hasProperty(key)) return project.property(key)
    else if (fallback != null) return fallback
    else throw new GradleException("Missing required property: ${key}")
}

repositories {
    repositories {
    maven { url = "https://maven.createmod.net" } 
    maven { url = "https://maven.ithundxr.dev/mirror" } 
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" } 
    maven { url = "https://maven.tterrag.dev/releases" } 
    maven { url = "https://maven.minecraftforge.net/" } 
    maven { url = "https://maven.theillusivec4.top/"}
    mavenCentral()
    }
}


def minecraft_version = requiredProp("minecraft_version")
def forge_version = requiredProp("forge_version")
def create_version = requiredProp("create_version")
def ponder_version = requiredProp("ponder_version")
def flywheel_version = requiredProp("flywheel_version")
def registrate_version = requiredProp("registrate_version")
def curios_version = requiredProp("curios_version")

def modId = requiredProp("mod_id")
def modName = requiredProp("mod_name")
def modVersion = requiredProp("mod_version")
def modGroupId = requiredProp("mod_group_id")
def modAuthors = requiredProp("mod_authors")
def modLicense = requiredProp("mod_license")
def modDescription = requiredProp("mod_description")

group = modGroupId
version = modVersion
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    implementation(fg.deobf("com.simibubi.create:create-${minecraft_version}:${create_version}:slim"))
    implementation(fg.deobf("net.createmod.ponder:Ponder-Forge-${minecraft_version}:${ponder_version}"))
    compileOnly(fg.deobf("dev.engine-room.flywheel:flywheel-forge-api-${minecraft_version}:${flywheel_version}"))
    runtimeOnly(fg.deobf("dev.engine-room.flywheel:flywheel-forge-${minecraft_version}:${flywheel_version}"))
    implementation(fg.deobf("com.tterrag.registrate:Registrate:${registrate_version}"))
    compileOnly "top.theillusivec4.curios:curios-forge:${curios_version}:api"
    runtimeOnly "top.theillusivec4.curios:curios-forge:${curios_version}"
    compileOnly annotationProcessor("io.github.llamalad7:mixinextras-common:0.4.1")
    implementation("io.github.llamalad7:mixinextras-forge:0.4.1")
}


minecraft {
    mappings channel: "official", version: minecraft_version
    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${modId}" { source sourceSets.main }
            }
        }

        client {
        
            property 'forge.enabledGameTestNamespaces', modId
            property("mixin.env.remapRefMap", "true")
            property("mixin.env.refMapRemappingFile", "${projectDir}/build/createSrgToMcp/output.srg")
        }

        server {
            property 'forge.enabledGameTestNamespaces', modId
            args '--nogui'
            property 'mixin.env.remapRefMap', 'true'
        }

        gameTestServer { property 'forge.enabledGameTestNamespaces', modId }

        data {
            workingDirectory project.file('run-data')
            args '--mod', modId, '--all',
                 '--output', file('src/generated/resources/'),
                 '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

tasks.named('processResources', ProcessResources).configure {
    def replaceProperties = [
        minecraft_version: minecraft_version,
        forge_version: forge_version,
        mod_id: modId,
        mod_name: modName,
        mod_license: modLicense,
        mod_version: modVersion,
        mod_authors: modAuthors,
        mod_description: modDescription
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes(
            'Specification-Title': modId,
            'Specification-Vendor': modAuthors,
            'Specification-Version': '1',
            'Implementation-Title': project.name,
            'Implementation-Version': project.jar.archiveVersion,
            'Implementation-Vendor': modAuthors,
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        )
    }
    finalizedBy 'reobfJar'
}

publishing {
    publications {
        register('mavenJava', MavenPublication) { artifact jar }
    }
    repositories {
        maven { url "file://${project.projectDir}/mcmodsrepo" }
    }
}
